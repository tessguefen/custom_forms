<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "TGCD">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Custom Data Sets">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Tess Guefen">
	<MvASSIGN NAME = "l.module:version"		VALUE = "1.000">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "9.00">
	<MvASSIGN NAME = "l.module:description"	VALUE = "I'll put a more descriptive description later.">
	<MvASSIGN NAME = "l.module:features"	VALUE = "data_store, system, util, json, clientside">
</MvFUNCTION>

<MvCOMMENT>

sXX_TGCD_Data
+--------------------+-----------+-----------+
|         id         |   code    |   name    |
+--------------------+-----------+-----------+
| TGCD_Data StoreKey | Form Code | Form Name |
+--------------------+-----------+-----------+


sXX_TGCD_Fields
+----------------------+--------------------+------+--------+-----------------------------------------+----------+---------------+
|          id          |      data_id       | code | prompt |                  type                   | required |  disp_order   |
+----------------------+--------------------+------+--------+-----------------------------------------+----------+---------------+
| TGCD_Fields StoreKey | Associated Data ID | Code | Prompt | text, textarea, radio, checkbox, select | 1/0      | Display Order |
+----------------------+--------------------+------+--------+-----------------------------------------+----------+---------------+

sXX_TGCD_Options
+-----------------------+---------------------+------+--------+---------------+
|          id           |      field_id       | code | prompt |  disp_order   |
+-----------------------+---------------------+------+--------+---------------+
| TGCD_Options StoreKey | Associated Field ID | Code | Prompt | Display Order |
+-----------------------+---------------------+------+--------+---------------+

sXX_TGCD_Values
+---------------------+--------------------------+--------------------+---------------------+-------+
|         id          |         group_id         |      data_id       |      field_id       | value |
+---------------------+--------------------------+--------------------+---------------------+-------+
| TGCD_Value StoreKey | TGCD_ValueGroup StoreKey | Associated Data ID | Associated Field ID | Value |
+---------------------+--------------------------+--------------------+---------------------+-------+


+-----------------------+
|       StoreKeys       |
+-----------------------+
| TGCD_Data             |
| TGCD_Fields_          |
| TGCD_Fields_DispOrder |
| TGCD_Options          |
| TGCD_Options_DisOrder |
| TGCD_Value            |
| TGCD_ValueGroup       |
+-----------------------+


</MvCOMMENT>

<MvFUNCTION NAME = "Module_Install_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Creation of sXX_TGCD_Data
	|
	</MvCOMMENT>

	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{	'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGCD_Data
					 				(
					 					id		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
					 					name	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ ',
					 					code	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ '
					 				) '}">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCD-INSTALL-0002:', 'An error occured while creating the table ' $ g.Store_Table_Prefix $ 'TGCD_Data. Please make sure this table was not already created.' ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	Creation of sXX_TGCD_Fields
	|
	</MvCOMMENT>

	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{	'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGCD_Fields
					 				(
					 					id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
					 					data_id		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
					 					code		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ ',
					 					prompt		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ ',
					 					type		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ ',
					 					required	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
					 					disp_order	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ '
					 				) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCD-INSTALL-0002:', 'An error occured while creating the table ' $ g.Store_Table_Prefix $ 'TGCD_Fields. Please make sure this table was not already created.' ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	Creation of sXX_TGCD_Options
	|
	</MvCOMMENT>

	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{	'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGCD_Options
					 				(
					 					id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
					 					field_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
					 					code		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ ',
					 					prompt		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ ',
					 					disp_order	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ '
					 				) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCD-INSTALL-0003:', 'An error occured while creating the table ' $ g.Store_Table_Prefix $ 'TGCD_Options. Please make sure this table was not already created.' ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	Creation of sXX_TGCD_Values
	|
	</MvCOMMENT>

	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{	'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGCD_Values
					 				(
					 					id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
					 					group_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
					 					data_id		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
					 					field_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
					 					value		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()			$ '
					 				) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCD-INSTALL-0004:', 'An error occured while creating the table ' $ g.Store_Table_Prefix $ 'TGCD_Values. Please make sure this table was not already created.' ) }">
	</MvIF>
	
	<MvCOMMENT>
	|
	|	Creation of StoreKeys
	|
	</MvCOMMENT>
	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGCD_Data', 1 ) }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCD-INSTALL-0005:', 'An error occured while creating the storekey, TGCD_Data. Please make sure this does not already exsist.' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGCD_Fields', 1 ) }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCD-INSTALL-0006:', 'An error occured while creating the storekey, TGCD_Fields. Please make sure this does not already exsist.' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGCD_Fields_DispOrder', 1 ) }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCD-INSTALL-0007:', 'An error occured while creating the storekey, TGCD_Fields_DispOrder. Please make sure this does not already exsist.' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGCD_Options', 1 ) }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCD-INSTALL-0008:', 'An error occured while creating the storekey, TGCD_Options. Please make sure this does not already exsist.' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGCD_Options_DispOrder', 1 ) }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCD-INSTALL-0009:', 'An error occured while creating the storekey, TGCD_Options_DispOrder. Please make sure this does not already exsist.' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGCD_Value', 1 ) }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCD-INSTALL-0010:', 'An error occured while creating the storekey, TGCD_Value. Please make sure this does not already exsist.' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGCD_ValueGroup', 1 ) }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCD-INSTALL-0011:', 'An error occured while creating the storekey, TGCD_ValueGroup. Please make sure this does not already exsist.' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Upgrade_Store" PARAMETERS = "module var, version" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Uninstall_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'TGCD_Data' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'TGCD_Fields' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'TGCD_Options' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'TGCD_Values' }">

	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGCD_Data' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGCD_Fields' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGCD_Fields_DispOrder' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGCD_Options' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGCD_Options_DispOrder' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGCD_Value' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGCD_ValueGroup' ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| system
|
</MvCOMMENT>

<MvFUNCTION NAME = "SystemModule_Action" PARAMETERS = "module var, action" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SystemModule_Screen" PARAMETERS = "module var, screen" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SystemModule_UIException" PARAMETERS = "module var, exception" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
|	util
|
</MvCOMMENT>

<MvFUNCTION NAME = "StoreUtilityModule_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_Action" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_Screen" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ ISNULL g.Admin_Open_Store }">							<MvFUNCTIONRETURN VALUE = 1>	</MvIF>

	<MvIF EXPR = "{ g.TGCD_Screen EQ 'Data_View' }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Data_Load_ID( g.Data_ID, l.data ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginScreen_Start( 'Data: ' $ l.data:name, '', '' ) }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_AdminUI_CSS() }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginScreen_End( 'Data: ' $ l.data:name, '', '' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginContent() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawTabs( 'TGCD', 'TGCD_Fields:Fields,TGCD_Values:Values' ) }">

		<MvHIDE FIELDS = "g.Data_ID,g.TGCD_Screen,g.Module_Code">

		<MvIF EXPR = "{ g.Tab EQ 'TGCD_Fields' }">
			<MvEVAL EXPR = "{ Data_Fields( l.module ) }">
		</MvIF>

		<MvIF EXPR = "{ g.Tab EQ 'TGCD_Values' }">
			<MvEVAL EXPR = "{ Data_Value( l.module ) }">
		</MvIF>

		<MvEVAL EXPR = "{ [ g.Module_Admin ].EndContent() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawButtons( '' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].EndScreen() }">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginScreen_Start( 'Custom Data', '', '' ) }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_AdminUI_CSS() }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginScreen_End( 'Custom Data', '', '' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginContent() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawTabs( 'TGCD', 'TGCD_Data:Custom Data' ) }">

		<MvHIDE FIELDS = "g.Module_Code">

		<MvIF EXPR = "{ g.Tab EQ 'TGCD_Data' }">
			<MvEVAL EXPR = "{ Data_Tab( l.module ) }">
		</MvIF>

		<MvEVAL EXPR = "{ [ g.Module_Admin ].EndContent() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawButtons( '' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].EndScreen() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_LeftNavigation"  PARAMETERS = "module var, indent" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ISNULL g.Admin_Open_Store }">							<MvFUNCTIONRETURN VALUE = 1>	</MvIF>
	<MvEVAL EXPR = "{ [ g.Module_Admin ].LeftNavigation_Dot( l.indent - 1, 'Screen=SUTL&Store_Code=' $ encodeattribute( g.Store:code ) $ '&Module_Code=' $ encodeattribute( l.module:code ), 'Main', 'Custom Data' ) }">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| json
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_JSON" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT><MvIF EXPR = "{ NOT g.Admin_Open_Store }"><MvFUNCTIONRETURN></MvIF></MvCOMMENT>
	<MvIF EXPR = "{ g.Module_Function EQ 'Data_Load_Query'}">		<MvFUNCTIONRETURN VALUE = "{ JSON_Data_Load_Query( l.module ) }">	</MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Data_Update'}">			<MvFUNCTIONRETURN VALUE = "{ JSON_Data_Update( l.module ) }">		</MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Data_Insert'}">			<MvFUNCTIONRETURN VALUE = "{ JSON_Data_Insert( l.module ) }">		</MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Data_Delete'}">			<MvFUNCTIONRETURN VALUE = "{ JSON_Data_Delete( l.module ) }">		</MvIF>

	<MvIF EXPR = "{ g.Module_Function EQ 'Fields_Load_Query' }">			<MvFUNCTIONRETURN VALUE = "{ JSON_Fields_Load_Query( l.module ) }">			</MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Field_Insert' }">					<MvFUNCTIONRETURN VALUE = "{ JSON_Field_Insert( l.module ) }">				</MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Field_Update' }">					<MvFUNCTIONRETURN VALUE = "{ JSON_Field_Update( l.module ) }">				</MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Field_Delete' }">					<MvFUNCTIONRETURN VALUE = "{ JSON_Field_Delete( l.module ) }">				</MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Field_DisplayOrder_Update' }">	<MvFUNCTIONRETURN VALUE = "{ JSON_Field_DisplayOrder_Update( l.module ) }">	</MvIF>

	<MvIF EXPR = "{ g.Module_Function EQ 'Option_Insert' }">		<MvFUNCTIONRETURN VALUE = "{ JSON_Option_Insert( l.module ) }">		</MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Option_Update' }">		<MvFUNCTIONRETURN VALUE = "{ JSON_Option_Update( l.module ) }">		</MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Option_Delete' }">		<MvFUNCTIONRETURN VALUE = "{ JSON_Option_Delete( l.module ) }">		</MvIF>

	<MvIF EXPR = "{ g.Module_Function EQ 'Load_Fields' }">				<MvFUNCTIONRETURN VALUE = "{ JSON_Load_Fields( l.module ) }">		</MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'JSON_Values_Load_Query' }">	<MvFUNCTIONRETURN VALUE = "{ JSON_Values_Load_Query( l.module ) }">	</MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Values_Insert' }">			<MvFUNCTIONRETURN VALUE = "{ JSON_Values_Insert( l.module ) }">		</MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Values_Update' }">			<MvFUNCTIONRETURN VALUE = "{ JSON_Values_Update( l.module ) }">		</MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Values_Delete' }">			<MvFUNCTIONRETURN VALUE = "{ JSON_Values_Delete( l.module ) }">		</MvIF>
</MvFUNCTION>

<MvCOMMENT>
|
| clientside
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Clientside" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, compresswhitespace" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Filename EQ 'Data_Batchlist.js' }">
		<MvEVAL EXPR = "{ [ g.Module_Clientside ].Module_Content_Type( l.module, 'text/javascript' ) }">
		<MvINCLUDE INTERPRET = "off" FILE = "js/Data_Batchlist.js">
	</MvIF>
	<MvIF EXPR = "{ g.Filename EQ 'Fields_Batchlist.js' }">
		<MvEVAL EXPR = "{ [ g.Module_Clientside ].Module_Content_Type( l.module, 'text/javascript' ) }">
		<MvINCLUDE INTERPRET = "off" FILE = "js/Fields_Batchlist.js">
	</MvIF>
	<MvIF EXPR = "{ g.Filename EQ 'Values_Batchlist.js' }">
		<MvEVAL EXPR = "{ [ g.Module_Clientside ].Module_Content_Type( l.module, 'text/javascript' ) }">
		<MvINCLUDE INTERPRET = "off" FILE = "js/Values_Batchlist.js">
	</MvIF>
</MvFUNCTION>

<MvINCLUDE FILE = "functions/data.mv">
<MvINCLUDE FILE = "functions/fields.mv">
<MvINCLUDE FILE = "functions/options.mv">
<MvINCLUDE FILE = "functions/values.mv">